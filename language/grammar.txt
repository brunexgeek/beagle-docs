! Structure
! ---------

Unit
	: Package? Import* Type+
	;

Package
	: "package" QualifiedName
	;

Import
	: "import" QualifiedName ( "." "*" | "as" Name )?
	;

Type
	: Class
	;

Class
	: Annotation* "class" QualifiedName Extends? ClassBody?
	;

Extends
	: ":" TypeReference ( "," TypeReference )*
	;

Annotation
	: "@" QualifiedName
	;

ClassBody
	: "{" TypeMember* "}"
	;

TypeMember
	: TypeVariable
	: TypeConstant
	: TypeFunction
	;

TypeVariable
	: Annotation* Variable
	;

TypeConstant
	: Annotation* Constant
	;

TypeFunction
	: Annotation* "def" Name ParameterList ( ":" TypeReference )? Block?
	;

ParameterList
	: "(" ")"
	: "(" Parameter ( "," Parameter )* ")"
	;

Parameter
	: ( "var" | "const" )? Name ":" TypeReference
	;

QualifiedName
	: Name ( "." Name )*
	;

TypeReference
	: QualifiedName ( "?" )?
	;

Name
	: <UTF-16 '_', 'a'-'z', 'A'-'Z' or '0'-'9'>
	;

! Expressions
! -----------

Expression
	: Disjunction ( AssignmentOperator Expression )*
	;

Disjunction
	: Conjunction ( "or" Disjunction )*
	;

Conjunction
	: EqualityComparison ( "and" Conjunction )*
	;

EqualityComparison
	: Comparison ( EqualityOperator EqualityComparison )*
	;

Comparison
	: NamedInfix ( ComparisonOperator Comparison )*
	;

NamedInfix
	: AdditiveExpression ( InOperator AdditiveExpression )*
	: AdditiveExpression IsOperator TypeReference
	;

AdditiveExpression
	: MultiplicativeExpression ( AdditiveOperator AdditiveExpression )*
	;

MultiplicativeExpression
	: PrefixUnaryExpression ( MultiplicativeOperator MultiplicativeExpression )*
	;

PrefixUnaryExpression
	: PrefixUnaryOperator? PostfixUnaryExpression
	;

PostfixUnaryExpression
	: AtomicExpression PostfixUnaryOperator?
	;

AtomicExpression
	: "(" Expression ")"
	: LiteralConstant
	: Name
	;

LiteralConstant
	: BooleanLiteral
	: StringLiteral
	: IntegerLiteral
	: "null"
	;

BooleanLiteral
	: "true"
	: "false"
	;

StringLiteral
	: <UTF-16 string>
	;

IntegerLiteral
	: <hexadecimal, decimal, octal or binary integral number>
	;

Character
	: <UTF-16 character>
	;

MultiplicativeOperator
	: "*"
	: "/"
	: "%"
	;

AdditiveOperator
	: "+"
	: "-"
	;

InOperator
	: "in"
	: "not" "in"
	;

IsOperator
	: "is"
	: "not" "is"
	;

ComparisonOperator
	: "<"
	: ">"
	: ">="
	: "<="
	;

EqualityOperator
	: "!="
	: "=="
	;

AssignmentOperator
	: "="
	: "+="
	: "-="
	: "*="
	: "/="
	: "%="
	: "&="
	: "|="
	: ">>="
	: "<<="
	;

PrefixUnaryOperator
	: "-"
	: "+"
	: "++"
	: "--"
	: "not"
	;

PostfixUnaryOperator
	: "++"
	: "--"
	# arguments for function call
	: ArgumentList   
	: ArrayAccess
	: MemberAccessOperator PostfixUnaryExpression
	;

MemberAccessOperator
	: "."
	;

ArgumentList
	: "(" ")"
	: "(" Argument ( "," Argument )* ")"
	;

Argument
	: ( Name "=" )? Expression
	;

ArrayAccess
	: "[" Expression ( "," Expression )* "]"
	;


! Statements
! ----------

Block
	: "{" Statement* "}"
	;

Statement
	: Variable
	: Constant
	: IfThenElse
	: Return
	: Expression
	;

Variable
	: "var" Name ( ":" TypeReference )? ( "=" Expression )?
	;

Constant
	: "const" Name ( ":" TypeReference )? "=" Expression
	;

IfThenElse
	: "if" Expression "then" BlockOrStatement ( "else" BlockOrStatement )?
	;

BlockOrStatement
	: Block
	: Statement
	;

Return
	: "return" Expression
	;

